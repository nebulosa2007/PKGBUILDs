# Maintainer: Matthew Sexton <mssxtn@gmail.com>
# Contributor: Nebulosa <nebulosa2007 at yandex dot ru>

pkgname=python-curl-cffi
_name=curl_cffi
pkgver=0.7.1
pkgrel=1
pkgdesc="Python binding for curl-impersonate via CFFI"
arch=(x86_64)
url="https://github.com/yifeikong/${_name}"
license=(MIT)
depends=(
  python-cffi
  python-certifi
  python-gevent
  python-eventlet
)
makedepends=(
  gcc13
  libcurl-impersonate-chrome
  python-build
  python-installer
  python-setuptools
  python-wheel
)
options=(!debug)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
b2sums=('5481d2c812583c34ac8eae8d4931281e01de2b2b4c5ac51d23ac3b3548e0b00b36224e53cd3b720c61d52c9bc4cd094c081c0a4729212134bed702168061daae')

build() {
  cd "${_name}-$pkgver"
  #Fix for gcc
  export CC=gcc-13
  export CXX=g++-13
  python -m build --wheel --no-isolation
}

package() {
  cd "${_name}-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 LICENSE  -t "$pkgdir"/usr/share/licenses/$pkgname/
}
