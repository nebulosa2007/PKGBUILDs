# Maintainer: Matthew Sexton <mssxtn@gmail.com>
# Contributor: Nebulosa <nebulosa2007 at yandex dot ru>

pkgname=python-curl-cffi
_name=curl_cffi
pkgver=0.7.1
pkgrel=3
pkgdesc="Python binding for curl-impersonate via CFFI"
arch=(x86_64)
url="https://github.com/lexiforest/$_name"
license=(MIT)
depends=(
  glibc
  libcurl-impersonate-chrome
  python
  python-cffi
  python-certifi
  python-gevent
  python-eventlet
)
makedepends=(
  gcc13
  python-build
  python-installer
  python-setuptools
  python-wheel
)
options=(!debug)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('0f03df8cf839084abbe115d559050daa4a0f463e40bf50d032152bd245d6e3ab72f09a11d2ef6c13c9348627421fc81a377733c476c366ba135e98f79419c91d')

prepare() {
  sed -i 's/libraries=get_curl_libraries()/libraries=["curl-impersonate-chrome"]/'                                         $_name-$pkgver/scripts/build.py
  sed -i '/^download_libcurl()$/d;/^    extra_objects=get_curl_archives(),$/d;/^    library_dirs=\[arch\["libdir"\]\],$/d' $_name-$pkgver/scripts/build.py
}

build() {
  cd "$_name-$pkgver"

  #Fix for gcc
  export CC=gcc-13
  export CXX=g++-13
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$_site_packages"/$_name-$pkgver.dist-info/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
