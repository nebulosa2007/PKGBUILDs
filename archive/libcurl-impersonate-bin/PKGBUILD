# Maintainer: mattf <matheusfillipeag@gmail.com>

pkgname=libcurl-impersonate-bin
pkgver=1.2.0
pkgrel=2
pkgdesc="Series of patches that make curl requests look like Chrome"
arch=(aarch64 armv7h i686 x86_64)
url="https://github.com/lexiforest/curl-impersonate"
license=(MIT)
depends=(glibc)
provides=(${pkgname%-bin}-chrome ${pkgname%-bin})
conflicts=(curl-impersonate)
options=(!debug)

source=(${url/github/raw\.githubusercontent}/master/LICENSE)
source_aarch64=($url/releases/download/v$pkgver/${pkgname%-bin}-v$pkgver.aarch64-linux-gnu.tar.gz)
source_armv7h=($url/releases/download/v$pkgver/${pkgname%-bin}-v$pkgver.arm-linux-gnueabihf.tar.gz)
source_i686=($url/releases/download/v$pkgver/${pkgname%-bin}-v$pkgver.i386-linux-gnu.tar.gz)
source_x86_64=($url/releases/download/v$pkgver/${pkgname%-bin}-v$pkgver.x86_64-linux-gnu.tar.gz)

sha256sums=('7d0c5f4b5b4c27299ad6759798772b866b3559ac5edb23845f36328eb734376d')
sha256sums_aarch64=('9409c19a8d7a1e88763b2c1281422d082fde21fae3b6615418995b61849fc753')
sha256sums_armv7h=('dabfb1b572c6fe37f32332a019e1048a5addb91a1a75ccdc4107ebeb10fc0c48')
sha256sums_i686=('6ac57d5ed305eee2887aa335216346e116c3fd3915a93a2a55b63efe9c6e385b')
sha256sums_x86_64=('f1aec526f2c28c816c261c22b9ad29c0eab39164a3e8e2b3f2c96d3a1b67b8dd')

package () {
  install -vDm 755 ${pkgname%-bin}.so.4.8.0 -t "$pkgdir"/usr/lib/
  ln -s /usr/lib/${pkgname%-bin}.so.4.8.0 "$pkgdir"/usr/lib/${pkgname%-bin}-chrome.so
  ln -s /usr/lib/${pkgname%-bin}.so.4.8.0 "$pkgdir"/usr/lib/${pkgname%-bin}-chrome.so.4
  ln -s /usr/lib/${pkgname%-bin}.so.4.8.0 "$pkgdir"/usr/lib/${pkgname%-bin}.so
  install -vDm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
